import org.jetbrains.intellij.tasks.RunPluginVerifierTask

plugins {
    id 'java'
    id 'org.jetbrains.intellij' version '0.6.5'
}

group 'com.vermouthx'
version '1.3.1'

repositories {
    mavenCentral()
}

compileJava {
    sourceCompatibility = 1.8
    targetCompatibility = 1.8
}

compileTestJava {
    sourceCompatibility = 1.8
    targetCompatibility = 1.8
}

intellij {
    version '2020.1'
    type 'IC'
}

buildSearchableOptions {
    enabled = false
}

patchPluginXml {
    sinceBuild("201.6668.113")
    untilBuild("211.*")

    def changelogFile = file("${project.buildDir}/CHANGELOG.html")
    def readmeFile = file("${project.buildDir}/README.html")

    if (changelogFile.exists()) {
        changeNotes changelogFile.text
    }
    if (readmeFile.exists()) {
        pluginDescription readmeFile.text
    }
}

runPluginVerifier {
    ideVersions = ["201.8743.12", "202.8194.7", "203.7148.57"]
    failureLevel = EnumSet.of(RunPluginVerifierTask.FailureLevel.COMPATIBILITY_PROBLEMS, RunPluginVerifierTask.FailureLevel.INVALID_PLUGIN)
}

publishPlugin {
    token System.getProperty("jetbrains.token")
}